<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN"
	 "http://www.w3.org/TR/html4/strict.dtd">

<html>

<head>

	<meta http-equiv="content-type" content="text/html;charset=utf-8">
	<link rel="stylesheet" type="text/css" href="../style/style.css">
	<script type="text/javascript" src="../include/jsmodeler.js"></script>
	<script type="text/javascript" src="../include/gputracer.js"></script>
	<script type="text/javascript" src="shapetracer.js"></script>
	<title>Basic Shape WebGL Path Tracer</title>
	
	<script id="fragmentshader" type="x-shader/fragment">
		precision highp float;

		varying vec2 vVertexPosition;
		uniform float uSize;

		#define EPS 0.00005
		#define BIGEPS 0.005
		#define PI 3.1415926535897932384626433832795
		#define INF 1.0 / 0.0

		uniform sampler2D uOriginalTextureSampler;

		uniform vec3 uCameraData[3];
		uniform vec3 uLightPosition;
		uniform float uLightRadius;
		uniform float uIteration;
		uniform int uPreview;

		struct Triangle
		{
			vec3 v0;
			vec3 v1;
			vec3 v2;
			vec3 n0;
			vec3 n1;
			vec3 n2;
			int mat;
		};

		struct Material
		{
			vec3 diffuse;
			float reflection;
		};

		struct Camera
		{
			vec3 eye;
			vec3 center;
			vec3 up;
		};

		struct Ray
		{
			vec3 origin;
			vec3 direction;
			float length;
		};
		
		struct Sphere
		{
			vec3 origin;
			float radius;
			Material material;
		};
		
		struct Box
		{
			vec3 min;
			vec3 max;
			Material material;
		};

		struct Plane
		{
			vec3 origin;
			vec3 normal;
		};
		
		struct Cylinder
		{
			vec3 origin;
			float radius;
			float height;
			Material material;
		};

		struct Intersection
		{
			vec3 position;
			float distance;
			vec3 normal;
			Material material;
		};

		uniform Box uRoomBox;
		#if SPHERE_COUNT > 0
			uniform Sphere uSpheres[SPHERE_COUNT];
		#endif
		#if BOX_COUNT > 0
			uniform Box uBoxes[BOX_COUNT];
		#endif
		#if CYLINDER_COUNT > 0
			uniform Cylinder uCylinders[CYLINDER_COUNT];
		#endif

		float Random (vec2 seed)
		{
			vec2 theSeed = seed + vec2 (uIteration, -uIteration);
			return fract (sin (dot (theSeed, vec2 (12.9898,78.233))) * 43758.5453);
		}

		vec3 GetSphereNormal (in Sphere sphere, in vec3 intersection)
		{
			vec3 normal = intersection - sphere.origin;
			return normalize (normal);
		}

		float RaySphereIntersection (in Ray ray, in Sphere sphere)
		{
			vec3 sphereToRay = ray.origin - sphere.origin;
			float a = dot (ray.direction, ray.direction);
			float b = 2.0 * dot (sphereToRay, ray.direction);
			float c = dot (sphereToRay, sphereToRay) - sphere.radius * sphere.radius;

			float disc = b * b - 4.0 * a * c;
			if (disc < EPS) {
				return INF;
			}

			float t0 = (-b - sqrt (disc)) * a * 0.5;
			float t1 = (-b + sqrt (disc)) * a * 0.5;
			float minT = min (t0, t1);
			float maxT = max (t0, t1);
			if (maxT < EPS || minT > maxT) {
				return INF;
			}

			if (minT > ray.length) {
				return INF;
			}
			return minT;
		}

		vec3 GetBoxNormal (in Box box, in vec3 intersection)
		{
			vec3 origin = (box.min + box.max) / 2.0;
			vec3 originToIntersection = (intersection - origin) / (box.max - box.min);

			float absX = abs (originToIntersection.x);
			float absY = abs (originToIntersection.y);
			float absZ = abs (originToIntersection.z);

			vec3 normal;
			normal.x = float (absX > absY && absX > absZ) * sign (originToIntersection.x);
			normal.y = float (absY > absX && absY > absZ) * sign (originToIntersection.y);
			normal.z = float (absZ > absX && absZ > absY) * sign (originToIntersection.z);
			return normalize (normal);
		}

		float RayBoxIntersection (in Ray ray, in Box box, in bool insideOut)
		{
			vec3 minT = (box.min - ray.origin) / ray.direction;
			vec3 maxT = (box.max - ray.origin) / ray.direction;
			
			vec3 minVec = min (minT, maxT);
			vec3 maxVec = max (minT, maxT);
			float tNear = max (max (minVec.x, minVec.y), minVec.z);
			float tFar = min (min (maxVec.x, maxVec.y), maxVec.z);
			
			if (tFar < EPS || tNear > tFar) {
				return INF;
			}
			
			float distance = insideOut ? tFar : tNear;
			if (distance > ray.length) {
				return INF;
			}
			return distance;
		}

		vec3 GetCylinderNormal (in Cylinder cylinder, in vec3 intersection)
		{
			if (abs (intersection.z - cylinder.origin.z - cylinder.height) < EPS) {
				return vec3 (0.0, 0.0, 1.0);
			} else if (abs (intersection.z - cylinder.origin.z) < EPS) {
				return vec3 (0.0, 0.0, -1.0);
			}
			vec3 normal = intersection - vec3 (cylinder.origin.xy, intersection.z);
			return normalize (normal);
		}

		float RayPlaneIntersection (in Ray ray, in Plane plane)
		{
			float denom = dot (ray.direction, plane.normal);
			if (denom > EPS) {
				return INF;
			}
			
			float distance = (dot (plane.origin - ray.origin, plane.normal)) / denom;
			if (distance < EPS || distance > ray.length) {
				return INF;
			}
			
			return distance;
		}

		float RayCylinderIntersection (in Ray ray, in Cylinder cylinder)
		{
			vec2 rayOrigin2D = ray.origin.xy - cylinder.origin.xy;
			vec2 rayDirection2D = normalize (ray.direction.xy);
			float a = rayDirection2D.x * rayDirection2D.x + rayDirection2D.y * rayDirection2D.y;
			float b = 2.0 * (rayOrigin2D.x * rayDirection2D.x + rayOrigin2D.y * rayDirection2D.y);
			float c = rayOrigin2D.x * rayOrigin2D.x + rayOrigin2D.y * rayOrigin2D.y - cylinder.radius * cylinder.radius;

			float disc = b * b - 4.0 * a * c;
			if (disc < EPS) {
				return INF;
			}

			float t0 = (-b - sqrt (disc)) * a * 0.5;
			float t1 = (-b + sqrt (disc)) * a * 0.5;
			float minT = min (t0, t1);
			float maxT = max (t0, t1);
			if (maxT < EPS || minT > maxT) {
				return INF;
			}

			float angle = acos (dot (ray.direction, vec3 (rayDirection2D, 0.0)));
			float distance = minT / cos (angle);
			if (distance < EPS) {
				return INF;
			}
			
			vec3 intersection = ray.origin + distance * ray.direction;
			if (intersection.z < cylinder.origin.z) {
				Plane plane;
				plane.origin = cylinder.origin;
				plane.normal = vec3 (0.0, 0.0, -1.0);
				float planeDistance = RayPlaneIntersection (ray, plane);
				vec3 planeIntersection = ray.origin + planeDistance * ray.direction;
				if (length (planeIntersection - plane.origin) > cylinder.radius) {
					return INF;					
				}
				distance = planeDistance;
			} else if (intersection.z > cylinder.origin.z + cylinder.height) {
				Plane plane;
				plane.origin = cylinder.origin + vec3 (0.0, 0.0, cylinder.height);
				plane.normal = vec3 (0.0, 0.0, 1.0);
				float planeDistance = RayPlaneIntersection (ray, plane);
				vec3 planeIntersection = ray.origin + planeDistance * ray.direction;
				if (length (planeIntersection - plane.origin) > cylinder.radius) {
					return INF;					
				}
				distance = planeDistance;
			}
			if (distance > ray.length) {
				return INF;
			}
			return distance;
		}

		vec3 GetReflectedDirection (in vec3 direction, in vec3 normal)
		{
			float dotProduct = dot (normal, direction);
			vec3 reflected = direction - normal * 2.0 * dotProduct;
			return normalize (reflected);
		}

		vec3 RandomDirOnSphere (in vec2 seed)
		{
			vec2 random = vec2 (Random (seed), Random (seed * 2.0)) * 6.283;
			vec3 randomDir = vec3 (sin (random.x) * vec2 (sin (random.y), cos (random.y)), cos (random.x));
			return normalize (randomDir);
		}

		vec3 RandomDirOnHemisphere (in vec3 normal, in vec2 seed)
		{
			vec3 randomDir = RandomDirOnSphere (seed);
			return (dot (randomDir, normal) < 0.0) ? -randomDir : randomDir;
		}

		vec3 PhongShading (in vec3 shadedPoint, in vec3 shadedPointNormal, in vec3 lightPosition, in Material shadedMaterial)
		{
			float lightDiffuseIntensity = 1.0;
			vec3 lightDirection = normalize (lightPosition - shadedPoint);
			float diffuseCoeff = max (dot (lightDirection, shadedPointNormal), 0.0);
			vec3 diffuseColor = shadedMaterial.diffuse * lightDiffuseIntensity;
			return diffuseColor * diffuseCoeff;
		}
		
		Intersection RayModelIntersection (in Ray ray)
		{
			Intersection result;
			result.distance = INF;
			#if SPHERE_COUNT > 0
				for (int i = 0; i < SPHERE_COUNT; i++) {
					float distance = RaySphereIntersection (ray, uSpheres[i]);
					if (distance < result.distance) {
						vec3 intersection = ray.origin + distance * ray.direction;
						result.position = intersection;
						result.normal = GetSphereNormal (uSpheres[i], intersection);
						result.distance = distance;
						result.material = uSpheres[i].material;
					}
				}
			#endif
			#if BOX_COUNT > 0
				for (int i = 0; i < BOX_COUNT; i++) {
					float distance = RayBoxIntersection (ray, uBoxes[i], false);
					if (distance < result.distance) {
						vec3 intersection = ray.origin + distance * ray.direction;
						result.position = intersection;
						result.normal = GetBoxNormal (uBoxes[i], intersection);
						result.distance = distance;
						result.material = uBoxes[i].material;
					}
				}
			#endif
			#if CYLINDER_COUNT > 0
				for (int i = 0; i < CYLINDER_COUNT; i++) {
					float distance = RayCylinderIntersection (ray, uCylinders[i]);
					if (distance < result.distance) {
						vec3 intersection = ray.origin + distance * ray.direction;
						result.position = intersection;
						result.normal = GetCylinderNormal (uCylinders[i], intersection);
						result.distance = distance;
						result.material = uCylinders[i].material;
					}
				}
			#endif

			float roomDistance = RayBoxIntersection (ray, uRoomBox, true);
			if (roomDistance < result.distance) {
				vec3 intersection = ray.origin + roomDistance * ray.direction;
				result.position = intersection;
				result.normal = -GetBoxNormal (uRoomBox, intersection);
				result.distance = roomDistance;
				result.material = uRoomBox.material;
			}
			return result;
		}

		vec3 TraceMainRay (in Ray ray)
		{
			vec3 shadedColor = vec3 (0.0, 0.0, 0.0);
			vec3 currentColor = vec3 (1.0, 1.0, 1.0);
			Ray currentRay = ray;
		
			for (int i = 0; i < 5; i++) {
				Intersection intersection = RayModelIntersection (currentRay);
				if (intersection.distance == INF) {
					break;
				}

				Material material = intersection.material;
				currentColor *= material.diffuse;

				vec3 randomLightPoint = uLightPosition + RandomDirOnSphere (intersection.position.xy) * uLightRadius;
				Ray lightRay;
				vec3 direction = normalize (randomLightPoint - intersection.position);
				lightRay.origin = intersection.position + direction * BIGEPS;
				lightRay.direction = direction;
				lightRay.length = length (randomLightPoint - intersection.position);
				Intersection lightIntersection = RayModelIntersection (lightRay);
				if (lightIntersection.distance == INF) {
					vec3 directLightColor = PhongShading (intersection.position, intersection.normal, randomLightPoint, material);
					shadedColor += currentColor * directLightColor;
				}
				
				currentRay.origin = intersection.position;
				if (Random (intersection.position.xy) < material.reflection) {
					currentRay.direction = GetReflectedDirection (currentRay.direction, intersection.normal);
					currentColor = vec3 (1.0, 1.0, 1.0);
				} else {
					currentRay.direction = RandomDirOnHemisphere (intersection.normal, intersection.position.xy);	
				};
				currentRay.length = INF;
			}
			return shadedColor;
		}

		Ray GetRay (in vec2 vertexPosition, in Camera camera, in float iteration)
		{
			vec3 eyeCenterDir = normalize (camera.center - camera.eye);
			vec3 horizontalDir = normalize (cross (eyeCenterDir, camera.up));
			vec3 verticalDir = normalize (cross (horizontalDir, eyeCenterDir ));

			float fieldOfView = PI / 3.0; // 60 degree
			float cameraDistance = 1.0 / tan (fieldOfView / 2.0);
			vec3 endPosition = camera.eye + eyeCenterDir * cameraDistance;
			
			float randomX = Random (vVertexPosition);
			float randomY = Random (-vVertexPosition);
			float pixelSize = 2.0 / uSize;
			endPosition = endPosition + horizontalDir * (vertexPosition.x + randomX * pixelSize);
			endPosition = endPosition + verticalDir * (vertexPosition.y + randomY * pixelSize);
			
			Ray result;
			result.origin = camera.eye;
			result.direction = normalize (endPosition - camera.eye);
			result.length = INF;
			return result;
		}

		void main (void)
		{
			Camera camera;
			camera.eye = uCameraData[0];
			camera.center = uCameraData[1];
			camera.up = uCameraData[2];
			
			vec2 originalPosition = (vVertexPosition + vec2 (1.0, 1.0)) / 2.0;;
			vec3 originalColor = texture2D (uOriginalTextureSampler, originalPosition).xyz;
			
			Ray ray = GetRay (vVertexPosition, camera, uIteration);
			vec3 shadedColor = TraceMainRay (ray);
			float weight = uIteration / (uIteration + 1.0);
			gl_FragColor = vec4 (mix (shadedColor, originalColor, weight), 1.0);
		}
	</script>
	
	<script type="text/javascript">
		window.onload = function () {
			var canvasElem = document.getElementById ('render');
			var controlsElem = document.getElementById ('controls');
			var fragmentShaderElem = document.getElementById ('fragmentshader');

			var shapeTracer = new ShapeTracer ();
			shapeTracer.Init (canvasElem, controlsElem, fragmentShaderElem);
		}
	</script>

</head>

<body>
	<a href="https://github.com/kovacsv/WebGLPathTrace">
		<img style="position: absolute; top: 0; right: 0; border: 0;" src="https://camo.githubusercontent.com/38ef81f8aca64bb9a64448d0d70f1308ef5341ab/68747470733a2f2f73332e616d617a6f6e6177732e636f6d2f6769746875622f726962626f6e732f666f726b6d655f72696768745f6461726b626c75655f3132313632312e706e67" alt="Fork me on GitHub" data-canonical-src="https://s3.amazonaws.com/github/ribbons/forkme_right_darkblue_121621.png">
	</a>

	<div class="frame">
		<div class="title">Basic Shape WebGL Path Tracer</div>
		<div class="text">
			<p>
				...
			</p>
		</div>
		<div class="container">
			<div class="canvas">
				<canvas id="render" width="512" height="512"></canvas>
			</div>
			<div class="controls" id="controls">
			</div>
		</div>
	</div>
</body>

</html>
