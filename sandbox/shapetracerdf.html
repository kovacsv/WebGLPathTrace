<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN"
	 "http://www.w3.org/TR/html4/strict.dtd">

<html>

<head>

	<meta http-equiv="content-type" content="text/html;charset=utf-8">
	<link rel="stylesheet" type="text/css" href="../style/style.css">
	<script type="text/javascript" src="../include/jsmodeler.js"></script>
	<script type="text/javascript" src="../include/gputracer.js"></script>
	<script type="text/javascript" src="shapetracerdf.js"></script>
	<title>Basic Shape WebGL Path Tracer</title>
	
	<script id="fragmentshader" type="x-shader/fragment">
		precision highp float;

		varying vec2 vVertexPosition;
		uniform float uSize;

		#define EPS 0.00005
		#define BIGEPS 0.005
		#define PI 3.1415926535897932384626433832795
		#define INF 1.0 / 0.0

		uniform sampler2D uOriginalTextureSampler;

		uniform vec3 uCameraData[3];
		uniform vec3 uLightPosition;
		uniform float uLightRadius;
		uniform float uIteration;
		uniform int uPreview;

		struct Triangle
		{
			vec3 v0;
			vec3 v1;
			vec3 v2;
			vec3 n0;
			vec3 n1;
			vec3 n2;
			int mat;
		};

		struct Material
		{
			vec3 diffuse;
			float reflection;
		};

		struct Camera
		{
			vec3 eye;
			vec3 center;
			vec3 up;
		};

		struct Ray
		{
			vec3 origin;
			vec3 direction;
			float length;
		};
		
		struct Shape
		{
			float type;
			vec3 origin;
			vec3 size;
			Material material;
		};

		struct Estimation
		{
			float distance;
			Material material;
		};

		struct Intersection
		{
			vec3 position;
			float distance;
			vec3 normal;
			Material material;
		};

		#if SHAPE_COUNT > 0
			uniform Shape uShapes[SHAPE_COUNT];
		#endif

		float Random (vec2 seed)
		{
			vec2 theSeed = seed + vec2 (uIteration, -uIteration);
			return fract (sin (dot (theSeed, vec2 (12.9898,78.233))) * 43758.5453);
		}

		vec3 GetReflectedDirection (in vec3 direction, in vec3 normal)
		{
			float dotProduct = dot (normal, direction);
			vec3 reflected = direction - normal * 2.0 * dotProduct;
			return normalize (reflected);
		}

		vec3 RandomDirOnSphere (in vec2 seed)
		{
			vec2 random = vec2 (Random (seed), Random (seed * 2.0)) * 6.283;
			vec3 randomDir = vec3 (sin (random.x) * vec2 (sin (random.y), cos (random.y)), cos (random.x));
			return normalize (randomDir);
		}

		vec3 RandomDirOnHemisphere (in vec3 normal, in vec2 seed)
		{
			vec3 randomDir = RandomDirOnSphere (seed);
			return (dot (randomDir, normal) < 0.0) ? -randomDir : randomDir;
		}

		vec3 PhongShading (in vec3 shadedPoint, in vec3 shadedPointNormal, in vec3 lightPosition, in Material shadedMaterial)
		{
			float lightDiffuseIntensity = 1.0;
			vec3 lightDirection = normalize (lightPosition - shadedPoint);
			float diffuseCoeff = max (dot (lightDirection, shadedPointNormal), 0.0);
			vec3 diffuseColor = shadedMaterial.diffuse * lightDiffuseIntensity;
			return diffuseColor * diffuseCoeff;
		}
		
		float PointSphereDistance (in vec3 point, in vec3 origin, in float radius)
		{
			vec3 offseted = point - origin;
			return length (offseted) - radius;
		}

		float PointBoxDistance (in vec3 point, in vec3 origin, in vec3 size)
		{
			vec3 offseted = point - origin;
			vec3 d = abs (offseted) - size;
			return min (max (d.x, max (d.y, d.z)), 0.0) + length (max (d, 0.0));
		}

		float PointRoomDistance (in vec3 point, in vec3 origin, in vec3 size)
		{
			return point.z + 1.0;
		}

		Estimation DistanceEstimation (in vec3 point)
		{
			Estimation result;
			result.distance = INF;
			for (int i = 0; i < SHAPE_COUNT; i++) {
				float distance = INF;
				if (uShapes[i].type == 1.0) {
					distance = PointSphereDistance (point, uShapes[i].origin, uShapes[i].size.x);
				} else if (uShapes[i].type == 2.0) {
					distance = PointBoxDistance (point, uShapes[i].origin, uShapes[i].size);
				} else if (uShapes[i].type == 3.0) {
					distance = PointRoomDistance (point, uShapes[i].origin, uShapes[i].size);
				}
				if (distance < result.distance) {
					result.distance = distance;
					result.material = uShapes[i].material;
				}
			}
			return result;
		}

		vec3 CalculateNormal (in vec3 point)
		{
			vec3 xDir = vec3 (EPS, 0.0, 0.0);
			vec3 yDir = vec3 (0.0, EPS, 0.0);
			vec3 zDir = vec3 (0.0, 0.0, EPS);
			vec3 normal = vec3 (
				DistanceEstimation (point + xDir).distance - DistanceEstimation (point - xDir).distance,
				DistanceEstimation (point + yDir).distance - DistanceEstimation (point - yDir).distance,
				DistanceEstimation (point + zDir).distance - DistanceEstimation (point - zDir).distance
			);
			return normalize (normal);
		}

		Intersection RayModelIntersection (in Ray ray)
		{
			Intersection result;
			result.distance = INF;
			const int maxSteps = 150;
			const float maxDistance = 50.0;
			
			vec3 shadedColor = vec3 (0.0, 0.0, 0.0);
			float distance = 0.0;
			for (int i = 0; i <= maxSteps; i++) {
				vec3 currentPoint = ray.origin + ray.direction * distance;
				Estimation estimation = DistanceEstimation (currentPoint);
				distance += estimation.distance;
				result.position = currentPoint;
				result.distance = distance;
				result.material = estimation.material;
				if (estimation.distance < EPS || distance > maxDistance) {
					break;
				}

			}
			if (distance > maxDistance) {
				result.distance = INF;
				return result;
			}
			result.normal = CalculateNormal (result.position);
			return result;
		}

		bool ShadowIntersection (in Ray ray)
		{
			return false;
		}

		vec3 TraceMainRay (in Ray ray)
		{
			vec3 shadedColor = vec3 (0.0, 0.0, 0.0);
			vec3 currentColor = vec3 (1.0, 1.0, 1.0);
			Ray currentRay = ray;
		
			for (int i = 0; i < 5; i++) {
				Intersection intersection = RayModelIntersection (currentRay);
				if (intersection.distance == INF) {
					break;
				}

				Material material = intersection.material;
				if (uPreview == 1) {
					shadedColor = PhongShading (intersection.position, intersection.normal, uLightPosition, material);
					break;
				}
				
				currentColor *= material.diffuse;

				vec3 randomLightPoint = uLightPosition + RandomDirOnSphere (intersection.position.xy) * uLightRadius;
				Ray lightRay;
				vec3 direction = normalize (randomLightPoint - intersection.position);
				lightRay.origin = intersection.position + direction * BIGEPS;
				lightRay.direction = direction;
				lightRay.length = length (randomLightPoint - intersection.position);
				if (RayModelIntersection (lightRay).distance == INF) {
					vec3 directLightColor = PhongShading (intersection.position, intersection.normal, randomLightPoint, material);
					shadedColor += currentColor * directLightColor;
				}
				
				if (Random (intersection.position.xy) < material.reflection) {
					currentRay.direction = GetReflectedDirection (currentRay.direction, intersection.normal);
					currentColor = vec3 (1.0, 1.0, 1.0);
				} else {
					currentRay.direction = RandomDirOnHemisphere (intersection.normal, intersection.position.xy);	
				}
				currentRay.origin = intersection.position + currentRay.direction * BIGEPS;
				currentRay.length = INF;
			}
			return shadedColor;
		}

		Ray GetRay (in vec2 vertexPosition, in Camera camera, in float iteration)
		{
			vec3 eyeCenterDir = normalize (camera.center - camera.eye);
			vec3 horizontalDir = normalize (cross (eyeCenterDir, camera.up));
			vec3 verticalDir = normalize (cross (horizontalDir, eyeCenterDir ));

			float fieldOfView = PI / 3.0; // 60 degree
			float cameraDistance = 1.0 / tan (fieldOfView / 2.0);
			vec3 endPosition = camera.eye + eyeCenterDir * cameraDistance;
			
			float randomX = Random (vVertexPosition);
			float randomY = Random (-vVertexPosition);
			float pixelSize = 2.0 / uSize;
			endPosition = endPosition + horizontalDir * (vertexPosition.x + randomX * pixelSize);
			endPosition = endPosition + verticalDir * (vertexPosition.y + randomY * pixelSize);
			
			Ray result;
			result.origin = camera.eye;
			result.direction = normalize (endPosition - camera.eye);
			result.length = INF;
			return result;
		}

		void main (void)
		{
			Camera camera;
			camera.eye = uCameraData[0];
			camera.center = uCameraData[1];
			camera.up = uCameraData[2];
			
			vec2 originalPosition = (vVertexPosition + vec2 (1.0, 1.0)) / 2.0;;
			vec3 originalColor = texture2D (uOriginalTextureSampler, originalPosition).xyz;
			
			Ray ray = GetRay (vVertexPosition, camera, uIteration);
			vec3 shadedColor = TraceMainRay (ray);
			float weight = uIteration / (uIteration + 1.0);
			gl_FragColor = vec4 (mix (shadedColor, originalColor, weight), 1.0);
		}
	</script>
	
	<script type="text/javascript">
		window.onload = function () {
			var canvasElem = document.getElementById ('render');
			var controlsElem = document.getElementById ('controls');
			var fragmentShaderElem = document.getElementById ('fragmentshader');

			var shapeTracer = new ShapeTracer ();
			shapeTracer.Init (canvasElem, controlsElem, fragmentShaderElem);
		}
	</script>

</head>

<body>
	<a href="https://github.com/kovacsv/WebGLPathTrace">
		<img style="position: absolute; top: 0; right: 0; border: 0;" src="https://camo.githubusercontent.com/38ef81f8aca64bb9a64448d0d70f1308ef5341ab/68747470733a2f2f73332e616d617a6f6e6177732e636f6d2f6769746875622f726962626f6e732f666f726b6d655f72696768745f6461726b626c75655f3132313632312e706e67" alt="Fork me on GitHub" data-canonical-src="https://s3.amazonaws.com/github/ribbons/forkme_right_darkblue_121621.png">
	</a>

	<div class="frame">
		<div class="title">Basic Shape WebGL Path Tracer</div>
		<div class="text">
			<p>
				...
			</p>
		</div>
		<div class="container">
			<div class="canvas">
				<canvas id="render" width="512" height="512"></canvas>
			</div>
			<div class="controls" id="controls">
			</div>
		</div>
	</div>
</body>

</html>
